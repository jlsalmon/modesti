buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE"
    }
}

plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "idea"
    id "eclipse"
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.3"
    id 'net.researchgate.release' version '2.3.4'
}

/** Until the spring-boot plugin is included at plugins.gradle.org, we have to use the old mechanism to include it */
apply plugin: "spring-boot"

group "cern.modesti"

sourceCompatibility = 1.8
targetCompatibility = 1.8

/**
 * Tell Spring Boot to make the JAR executable by appending a script to the JAR with
 * start/stop/restart/status commands
 */
springBoot {
    executable = true
    layout = 'ZIP'
}

/**
 * Make a tarball containing the executable fat jar created by bootRepackage, and everything
 * in the src/dist directory
 */
task fatJarTar(type: Tar, dependsOn: 'bootRepackage') {
    compression Compression.GZIP
    extension "tar.gz"

    into('lib') {
        from libsDir
        include project.name + "-" + version + ".jar"
    }
    from 'src/dist'
}

/** Add version information to the manifest file */
jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}

/** Repositories used when searching for dependencies */
repositories {
    mavenCentral()
    maven { url "http://artifactory/release" }
    maven { url "http://artifactory/development" }
    maven { url "https://repo.spring.io/release" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/snapshot" }
}

dependencies {

    compile "org.springframework.boot:spring-boot-starter-data-rest"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-mail"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.security:spring-security-ldap:4.0.2.RELEASE"
    //compile "org.springframework.data:spring-data-rest-webmvc:2.4.1.RELEASE"
    //compile "org.springframework.boot:spring-boot:1.3.0.RC1"
    compile "org.activiti:activiti-spring-boot-starter-basic:5.18.0"
    compile "org.activiti:activiti-spring-boot-starter-jpa:5.18.0"
    compile "org.activiti:activiti-ldap:5.18.0"
    compile "org.springframework.plugin:spring-plugin-core:1.2.0.RELEASE"
    compile "org.apache.poi:poi:3.12-beta1"
    compile "org.apache.poi:poi-ooxml:3.12-beta1"
    compile "com.mysema.querydsl:querydsl-mongodb:3.6.6"
    compile "com.mysema.querydsl:querydsl-jpa:3.6.6"
    compile "cz.jirutka.rsql:rsql-parser:2.0.0"
    //compile 'com.github.fge:json-patch:1.9'
    compile 'de.danielbechler:java-object-diff:0.93'
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.6.1"
    compile "org.apache.directory.server:apacheds-server-jndi:1.5.5"
    compile "org.apache.commons:commons-lang3:3.4"
    compile "org.projectlombok:lombok:1.16.4"
    compile "com.h2database:h2"
    compile "oracle:ojdbc:11.2.0.3"
    compile "com.github.fakemongo:fongo:1.6.3"
    compile "com.google.code.gson:gson:2.3.1"
    compile "ch.qos.logback:logback-classic:1.1.2"
    compile "org.slf4j:slf4j-api:1.7.12"

    testCompile "cern.modesti:modesti-test:0.1.4"
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.5"
}

/** Generate QueryDsl classes */
querydsl {
    library = "com.mysema.querydsl:querydsl-apt:3.6.6"
    querydslSourcesDir = "src/querydsl/java"
    springDataMongo = true
    jpa = true
}

/** Configure publishing to Artifactory */
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                def parentNode = asNode().appendNode('parent')
                parentNode.appendNode('groupId', 'org.springframework.boot')
                parentNode.appendNode('artifactId', 'spring-boot-starter-parent')
                parentNode.appendNode('version', '1.3.0.M5')
            }
            artifact(fatJarTar) {
                classifier "dist"
            }
        }
    }
    repositories {
        maven {
            credentials {
                username artifactoryUser
                password artifactoryPassword
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                url "http://artifactory/beco-development-local"
            } else {
                url "http://artifactory/beco-release-local"
            }
        }
    }
}

/** Configure release options */
afterReleaseBuild.dependsOn publish
release {
    preTagCommitMessage = 'Set version before release: '
    tagCommitMessage = 'Tagging version '
    newVersionCommitMessage = 'Bumped version to '
}

