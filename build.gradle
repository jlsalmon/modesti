buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:2.1.0'
    }
}

plugins {
    id "io.spring.dependency-management" version "0.6.1.RELEASE"
    id "net.researchgate.release" version "2.5.0"
    id "org.sonarqube" version "2.5"
}

subprojects {
    group = 'cern.modesti'

    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "org.owasp.dependencycheck"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyCheck {
        format="XML"
    }
	
    sonarqube {
        properties {
            property "sonar.dependencyCheck.reportPath", "build/reports/dependency-check-report.xml"
        }
    }
	
    repositories {
        mavenCentral()
        maven { url "http://artifactory/release" }
        maven { url "http://artifactory/development" }
        maven { url "https://repo.spring.io/release" }
    }

    dependencyManagement {
        applyMavenExclusions = false
    }

    /** Add version information to the manifest file */
    jar {
        compileJava.doLast {
            manifest {
                attributes("Implementation-Version": version)
            }
        }
    }
}

release {
    buildTasks = ['releaseBuild']
}

task releaseBuild {
    dependsOn(
            'modesti-api:build',
            'modesti-test:build',
            'modesti-server:build'
    )
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.10"
}
